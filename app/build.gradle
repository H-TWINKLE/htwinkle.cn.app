apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

ext {
    verCode = 1
    verName = "1.0"
}

def addVerCode() {
    //如果version.properties文件可读则执行操作
    def versionPropsFile = file('../version.properties')
    if (versionPropsFile.canRead()) {
        //载入version.properties
        println("find file: VERSION_CODE")
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        //从version.properties根据关键字"VERSION_CODE"取Integer
        verCode = versionProps['VERSION_CODE'].toInteger()
        verName = versionProps['VERSION_NAME'].toString()
        println("find verCode: " + verCode + " find verName: " + verName)
        //判断build模式,如果是特定模式
        // 则自增版本号,并且写回version.properties文件中
        def runTasks = gradle.startParameter.taskNames
        //根据实际情况进行处理
        if ('assemble' in runTasks) {
            println("auto add: VERSION_CODE")
            //版本号自增并写入版本控制文件
            versionProps['VERSION_CODE'] = (++verCode).toString()
            verCode = versionProps['VERSION_CODE'].toInteger()
            versionProps.store(versionPropsFile.newWriter(), null)
        }
    } else {
        throw new GradleException("Could not find version.properties!")
    }
    //版本号及版本名自增逻辑结束
}

android {
    addVerCode()

    compileSdk 31

    defaultConfig {
        applicationId "cn.htwinkle.app"
        minSdk 24
        targetSdk 28
        versionCode verCode
        versionName verName + "." + verCode

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            manifestPlaceholders = [
                    //这里也是在AndroidManifest.xml中配置的app图标，同样在这里写了了不同的资源文件，从而实现了动态配置
                    app_icon: "@drawable/main_logo",
                    app_name: "@string/global_app_name"
            ]
        }

        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [
                    //这里也是在AndroidManifest.xml中配置的app图标，同样在这里写了了不同的资源文件，从而实现了动态配置
                    app_icon: "@drawable/main_logo_debug",
                    app_name: "@string/global_app_name_debug"
            ]
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "WeLife_V${defaultConfig.versionName}_${variant.buildType.name}.apk"
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // andorid 框架
    implementation 'org.xutils:xutils:3.9.0'

    implementation 'com.alibaba:fastjson:1.2.62'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.11'

    // 扫码
    implementation 'com.github.yuzhiqiang1993:zxing:2.1.1'

    // imageview
    implementation 'com.github.loopj:android-smart-image-view:master-d09783b950-1'

    implementation 'com.squareup.okio:okio:1.14.0'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // 解析
    implementation 'org.jsoup:jsoup:1.13.1'

    // 权限
    implementation 'com.github.getActivity:XXPermissions:16.2'

    // https://mvnrepository.com/artifact/cn.hutool/hutool-all
    implementation 'cn.hutool:hutool-all:5.8.22'

    // 屏幕录制
    // implementation 'com.github.nanchen2251:ScreenRecordHelper:1.0.2'
    implementation 'com.github.pedroSG94.RootEncoder:library:2.3.5'
    //implementation 'com.github.pedroSG94.vlc-example-streamplayer:pedrovlc:2.5.14v3'
    //implementation 'com.github.tcking:giraffeplayer2:0.1.25'

    //implementation 'com.github.tcking:ijkplayer-arm64:0.8.8'
    //implementation 'com.github.tcking:ijkplayer-x86_64:0.8.8' //support x86_64

    implementation 'io.github.justson:agentweb-core:v5.1.1-androidx'

    // android 自动更新
    implementation 'com.qianwen:update-app:3.5.2'
}